
use ki:http;
use ki:json;
use ki:sys;

shared_global i32 date_race = 0;
shared_global bool date_expired = true;
shared_global String date = "";

func reset_date() void {
	sys:sleep_ms(950);
	date_race = 0;
	date_expired = true;
}

func update_date() void {
	@ x = atomicop date_race ADD 1;
	if(x == 0){
		date_expired = false;
		date = http:get_date();
		@ task = async reset_date();
	}
}

func handler(http:Request req) http:Response {

	if(date_expired){
		update_date();
	}

	@ headers = Map<String>{};
	headers.set("Server", "ki-http");
	headers.set("Date", date);

	if(req.uri == "/plaintext") {
		return http:Response.text("Hello, World!", 200, "text/plain", headers);
	} else if(req.uri == "/json") {
		@ dec = json:decode("{\"message\":\"Hello, World!\"}") or { return http:Response.error(500); };
		@ enc = json:encode(dec);
		return http:Response.text(enc, 200, "application/json", headers);
	}

	return http:Response.error(404);
}

func main() i32 {

	date = http:get_date();

	@ s = http:Server.init("0.0.0.0", 8080, handler, 50000) or { panic "Failed to start server"; };
	s.start();

	return 0;
}
