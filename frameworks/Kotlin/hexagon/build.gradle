
plugins {
    id "org.jetbrains.kotlin.jvm" version "1.6.10"
}

ext {
    gradleScripts = "https://raw.githubusercontent.com/hexagonkt/hexagon/2.0.0-B1/gradle"

    hexagonVersion = "2.0.0-B1"
    hikariVersion = "5.0.0"
    jettyVersion = "11.0.7"
    postgresqlVersion = "42.3.1"
    cache2kVersion = "2.4.1.Final"
}

apply(from: "$gradleScripts/kotlin.gradle")
apply(from: "$gradleScripts/application.gradle")

apply(plugin: "war")

defaultTasks("installDist")

application {
    mainClass.set("com.hexagonkt.BenchmarkKt")
}

war {
    archiveFileName = "ROOT.war"
}

installDist.dependsOn("war")

dependencies {
    implementation("com.hexagonkt:http_server_jetty:$hexagonVersion")
    implementation("com.hexagonkt:templates_pebble:$hexagonVersion")
    implementation("com.hexagonkt:logging_slf4j_jul:$hexagonVersion")
    implementation("com.hexagonkt:serialization_jackson_json:$hexagonVersion")

    implementation("org.cache2k:cache2k-core:$cache2kVersion")
    implementation("com.zaxxer:HikariCP:$hikariVersion")
    implementation("org.postgresql:postgresql:$postgresqlVersion")

    // providedCompile excludes the dependency only in the WAR, not in the distribution
    providedCompile("org.eclipse.jetty:jetty-webapp:$jettyVersion") { exclude module: "slf4j-api" }
}
