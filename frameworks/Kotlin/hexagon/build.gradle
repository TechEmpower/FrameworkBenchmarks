
plugins {
    id "org.jetbrains.kotlin.jvm" version "1.7.10"
}

ext {
    hexagonVersion = "2.0.17"
    hikariVersion = "5.0.1"
    jettyVersion = "11.0.11"
    postgresqlVersion = "42.4.0"
    cache2kVersion = "2.6.1.Final"
    nettyVersion = "4.1.79.Final"

    gradleScripts = "https://raw.githubusercontent.com/hexagonkt/hexagon/$hexagonVersion/gradle"
}

apply(from: "$gradleScripts/kotlin.gradle")
apply(from: "$gradleScripts/application.gradle")

apply(plugin: "war")

defaultTasks("installDist")

application {
    mainClass.set("com.hexagonkt.BenchmarkKt")
}

war {
    archiveFileName = "ROOT.war"
}

installDist.dependsOn("war")

dependencies {
    implementation("com.hexagonkt:http_server_netty_epoll:$hexagonVersion")
    implementation("com.hexagonkt:http_server_jetty:$hexagonVersion")
    implementation("com.hexagonkt:templates_pebble:$hexagonVersion")
    implementation("com.hexagonkt:logging_slf4j_jul:$hexagonVersion")
    implementation("com.hexagonkt:serialization_jackson_json:$hexagonVersion")

    implementation("io.netty:netty-transport-native-epoll:$nettyVersion:linux-x86_64")
    implementation("org.cache2k:cache2k-core:$cache2kVersion")
    implementation("com.zaxxer:HikariCP:$hikariVersion")
    implementation("org.postgresql:postgresql:$postgresqlVersion")

    // providedCompile excludes the dependency only in the WAR, not in the distribution
    providedCompile("org.eclipse.jetty:jetty-webapp:$jettyVersion") { exclude module: "slf4j-api" }
}

task("minimizeTemplate") {
    doLast {
        File template = file("$buildDir/resources/main/fortunes.pebble.html")
        List<String> lines = template.readLines().collect { it.trim() }
        template.write(lines.join(""))
    }
}

assemble.dependsOn("minimizeTemplate")
