import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import org.gradle.api.tasks.testing.logging.TestLogEvent.FAILED
import org.gradle.api.tasks.testing.logging.TestLogEvent.PASSED
import org.gradle.api.tasks.testing.logging.TestLogEvent.SKIPPED
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.dsl.KotlinVersion

tasks.wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

plugins {
    kotlin("jvm") version "2.3.0"
    application
    id("nu.studer.rocker") version "3.2"
    id("com.gradleup.shadow") version "9.3.0"
}

group = "io.vertx"
version = "5.0.5" // kept consistent with the "vert-web" portion

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.10.2")
    implementation(platform("io.vertx:vertx-stack-depchain:$version"))
    implementation("io.vertx:vertx-core")
    implementation("io.vertx:vertx-launcher-application")
    implementation("com.fasterxml.jackson.module:jackson-module-blackbird:2.19.0") // kept consistent with the "vert-web" portion
    implementation("io.vertx:vertx-web")
    implementation("io.vertx:vertx-pg-client")
    implementation("io.vertx:vertx-web-templ-rocker")
    //runtimeOnly("io.netty", "netty-transport-native-epoll", classifier = "linux-x86_64")
    runtimeOnly("io.netty", "netty-transport-native-io_uring", classifier = "linux-x86_64")
    implementation("io.vertx:vertx-lang-kotlin")
    implementation("io.vertx:vertx-lang-kotlin-coroutines")
}

rocker {
    configurations {
        create("main") {
            templateDir.set(file("src/main/resources"))
            optimize.set(true)
            javaVersion.set("25") // kept consistent with the "vert-web" portion
        }
    }
}

kotlin.jvmToolchain(25) // kept consistent with the "vert-web" portion


// content below copied from the project generated by the app generator

val mainVerticleName = "io.vertx.benchmark.App"
val launcherClassName = "io.vertx.launcher.application.VertxApplication"

application {
    mainClass.set(launcherClassName)
}

tasks.withType<ShadowJar> {
    archiveClassifier.set("fat")
    manifest {
        attributes(mapOf("Main-Verticle" to mainVerticleName))
    }
    mergeServiceFiles()
}

tasks.withType<JavaExec> {
    args = listOf(mainVerticleName)
}
