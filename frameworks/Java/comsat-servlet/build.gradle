// Capsule plugin
plugins {
    id "us.kirchmeier.capsule" version "1.0.2"
}

apply plugin: 'java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = '0.3'

ext.jacksonVer = '2.7.0'
ext.quasarVer  = '0.7.4'
ext.comsatVer  = '0.7.0'
ext.capsuleVer = '1.0.1'

ext.jettyVer   = '9.3.8.v20160314'

[compileJava, compileTestJava]*.options*.encoding = "UTF-8"

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    // mavenLocal()
}

configurations {
    quasar
}

configurations.all {
    resolutionStrategy {
        failOnVersionConflict()
        force "org.eclipse.jetty:jetty-util:$jettyVer"
        force "org.eclipse.jetty:jetty-io:$jettyVer"
        force "org.eclipse.jetty:jetty-http:$jettyVer"
        force "org.eclipse.jetty:jetty-server:$jettyVer"
        force "org.eclipse.jetty:jetty-servlet:$jettyVer"
        force "org.eclipse.jetty:jetty-webapp:$jettyVer"
        force "org.eclipse.jetty:jetty-xml:$jettyVer"
        force "org.eclipse.jetty.websocket:websocket-server:$jettyVer"
        force "org.eclipse.jetty.websocket:websocket-servlet:$jettyVer"
        force "org.eclipse.jetty.websocket:websocket-client:$jettyVer"
    }
}

configurations.capsule.dependencies.clear()

dependencies {
    compile "co.paralleluniverse:quasar-core:$quasarVer:jdk8"
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVer"
    compile "co.paralleluniverse:comsat-servlet:$comsatVer"
    compile "co.paralleluniverse:comsat-test-utils:$comsatVer"

    capsule "co.paralleluniverse:capsule:$capsuleVer"

    quasar "co.paralleluniverse:quasar-core:$quasarVer:jdk8"
}

task capsule(type: FatCapsule) {
    applicationClass "hello.World"

    capsuleManifest {
        javaAgents = [configurations.quasar.iterator().next().getName()]

        mode('undertow') {
            systemProperties['serverClass'] = 'hello.UndertowServer'
        }

        mode('jetty') {
            systemProperties['serverClass'] = 'hello.JettyServer'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}
