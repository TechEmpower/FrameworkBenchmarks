// Capsule plugin
plugins {
    id "us.kirchmeier.capsule" version "1.0.2"
}

apply plugin: 'java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = '0.3'

ext.jacksonVer  = '2.7.4'
ext.quasarVer   = '0.7.5'
ext.comsatVer   = '0.7.0'
ext.capsuleVer  = '1.0.2'

ext.nettyVer    = '4.0.37.Final'
ext.undertowVer = '1.3.22.Final'

ext.slf4jVer    = '1.7.21'

[compileJava, compileTestJava]*.options*.encoding = "UTF-8"

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    // mavenLocal()
}

configurations {
    quasar
}

configurations.all {
    resolutionStrategy {
        failOnVersionConflict()
        force "io.netty:netty-all:$nettyVer"
        force "io.undertow:undertow-core:$undertowVer"
        force "org.slf4j:slf4j-api:$slf4jVer"
        force "co.paralleluniverse:quasar-actors:$quasarVer"
    }
}

configurations.capsule.dependencies.clear()

dependencies {
    compile "co.paralleluniverse:quasar-core:$quasarVer:jdk8"
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVer"
    compile "co.paralleluniverse:comsat-actors-api:$comsatVer"
    compile "co.paralleluniverse:comsat-actors-netty:$comsatVer"
    compile "co.paralleluniverse:comsat-actors-undertow:$comsatVer"
    compile "co.paralleluniverse:comsat-test-utils:$comsatVer"

    capsule "co.paralleluniverse:capsule:$capsuleVer"

    quasar "co.paralleluniverse:quasar-core:$quasarVer:jdk8"
}

task capsule(type: FatCapsule) {
    applicationClass "hello.World"

    capsuleManifest {
        javaAgents = [configurations.quasar.iterator().next().getName()]

        mode('netty') {
            systemProperties['serverClass'] = 'hello.NettyServer'
        }

        mode('undertow') {
            systemProperties['serverClass'] = 'hello.UndertowServer'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}
